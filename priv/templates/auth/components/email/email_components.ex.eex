defmodule <%= @web_module %>.Components.Email.EmailComponents do
  @moduledoc """
  Provides email layouts and components for use across all contexts.

  This module can be used by any context that needs to send emails,
  ensuring consistent styling and structure across the application.

  ## Usage in your notifier modules

      defmodule MyApp.MyContext.Notifier do
        use <%= @web_module %>, :html
        import <%= @web_module %>.Components.Email.EmailComponents

        # Your email functions here using ~H sigil
      end
  """
  use Phoenix.Component

  embed_templates "layouts/*"

  # Load logo at compile time and encode to base64
  @logo_path Path.join([:code.priv_dir(:<%= @app_name %>), "static", "images", "logo_small.png"])
  @logo_base64 (if File.exists?(@logo_path) do
                 @logo_path |> File.read!() |> Base.encode64()
               else
                 ""
               end)

  @app_name Application.compile_env(:<%= @app_name %>, :app_name, "<%= @app_module %>")

  @doc """
  Returns the base64-encoded logo for use in email templates.
  """
  def logo_base64, do: @logo_base64

  @doc """
  Returns the app name for use in email templates.
  """
  def app_name, do: @app_name

  @doc """
  Converts a HEEx body to a binary string.
  """
  def body(heex_body) do
    heex_body
    |> Phoenix.HTML.Safe.to_iodata()
    |> IO.iodata_to_binary()
  end

  @doc """
  Renders a button component for emails.

  ## Examples

      <.email_button href={@url}>
        Click here
      </.email_button>
  """
  attr :href, :string, required: true
  slot :inner_block, required: true

  def email_button(assigns) do
    ~H"""
    <table role="presentation" cellspacing="0" cellpadding="0" border="0" style="margin: 0 0 24px;">
      <tr>
        <td style="border-radius: 6px; background-color: #000000;">
          <a
            href={@href}
            style="display: inline-block; padding: 14px 32px; font-size: 16px; font-weight: 600; color: #ffffff; text-decoration: none;"
          >
            {render_slot(@inner_block)}
          </a>
        </td>
      </tr>
    </table>
    <table
      role="presentation"
      cellspacing="0"
      cellpadding="0"
      border="0"
      style="width: 100%; margin: 0 0 24px;"
    >
      <tr>
        <td style="border-left: 4px solid #e5e5e5; padding: 16px;">
          <p style="margin: 0; font-size: 14px; color: #666666; font-style: italic;">
            If the button is not working, copy and paste this URL in your browser:
          </p>
          <p style="margin: 4px 0 0; font-size: 14px; color: #666666; font-style: italic; text-decoration: underline; word-break: break-all;">
            {@href}
          </p>
        </td>
      </tr>
    </table>
    """
  end

  @doc """
  Renders a heading for emails.

  ## Examples

      <.heading>Welcome!</.heading>
  """
  slot :inner_block, required: true

  def heading(assigns) do
    ~H"""
    <h1 style="margin: 0 0 24px; font-size: 24px; font-weight: 600; color: #000000;">
      {render_slot(@inner_block)}
    </h1>
    """
  end

  @doc """
  Renders a paragraph for emails.

  ## Examples

      <.p>Your email content here.</.p>
  """
  slot :inner_block, required: true

  def p(assigns) do
    ~H"""
    <p style="margin: 0 0 16px;">
      {render_slot(@inner_block)}
    </p>
    """
  end

  @doc """
  Renders a muted/secondary text paragraph for emails.

  ## Examples

      <.muted>This link will expire in 7 days.</.muted>
  """
  slot :inner_block, required: true

  def muted(assigns) do
    ~H"""
    <p style="margin: 0 0 16px; font-size: 14px; color: #666666;">
      {render_slot(@inner_block)}
    </p>
    """
  end
end
